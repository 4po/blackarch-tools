# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='chiron'
pkgver='0.8.1'
pkgrel=1
groups=('blackarch' 'blackarch-scanner' 'blackarch-networking'
        'blackarch-exploitation' 'blackarch-proxy')
pkgdesc='An all-in-one IPv6 Penetration Testing Framework.'
arch=('any')
url='http://www.secfu.net/tools-scripts/'
license=('GPL2')
depends=('python2' 'scapy')
source=("http://www.secfu.net/app/download/9941698297/Chiron_${pkgver}.tar.gz")
sha1sums=('411a979d61d453663e64b854238326015d1f474d')


package() {
  cd "$srcdir/Chiron_$pkgver"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/chiron"

  install -Dm644 Chiron_Tutorial.pdf \
    "$pkgdir/usr/share/doc/chiron/Chiron_Tutorial.pdf"
  install -Dm644 licence.txt "$pkgdir/usr/share/licenses/chiron/license.txt"

  rm Chiron_Tutorial.pdf licence.txt

  cp -a bin/* lib/* files/ "$pkgdir/usr/share/chiron/"
  ln -s files/* "$pkgdir/usr/share/chiron/."

  local tools="chiron-combinations chiron-nd chiron-proxy chiron-scanner"
  local tools="$tools chiron-attacks chiron-local-link"

  for tool in ${tools}
  do
    cat > "$pkgdir/usr/bin/$tool" << EOF
#!/bin/sh
cd /usr/share/chiron
exec python2 ${tool/-/_}.py "\$@"
EOF
    chmod +x "$pkgdir/usr/bin/$tool"
  done

  chmod 755 "$pkgdir/usr/share/chiron/"*.py
}
