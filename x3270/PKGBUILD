# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='x3270'
pkgver='3.4ga9'
pkgrel=1
pkgdesc='An IBM 3270 terminal emulator for the X Window System.'
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='http://x3270.bgp.nu/'
license=('BSD' 'MIT')
depends=('openssl' 'libxaw')
makedepends=('imake' 'openssl' 'libx11' 'libxaw' 'libxt' 'xbitmaps'
             'xorg-mkfontdir')
backup=('etc/x3270/ibm_hosts')
install='x3270.install'
source=("http://downloads.sourceforge.net/project/x3270/x3270/3.4ga9/suite3270-${pkgver}-src.tgz")
sha1sums=('3e2b3b35bb166a8de60eafbb85c569e198f632b6')

build() {
  cd "$srcdir/suite3270-${pkgver:0:3}"

  ./configure --prefix=/usr --bindir=/usr/bin --sbindir=/usr/bin \
    --sysconfdir=/etc --with-fontdir=/usr/share/fonts/3270

  make depend

  make
}

package() {
  cd "$srcdir/suite3270-${pkgver:0:3}"

  mkdir -p "$pkgdir/usr/share/fonts/3270/fonts.dir"

  make DESTDIR="$pkgdir" install

  install -Dm644 x3270-icon2.xpm "$pkgdir/usr/share/pixmaps/x3270-icon2.xpm"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/x3270/LICENSE"

  #chmod 644 "$pkgdir/etc/x3270/ibm_hosts"
}
